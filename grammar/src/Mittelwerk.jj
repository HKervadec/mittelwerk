PARSER_BEGIN(Mittelwerk)
package Core;

import Ident.*;
import Utils.*;
import Manager.*;
import Emitter.*;
import Emitter.Instruction.*;
import Error.*;


public class Mittelwerk {
	public static IdentManager i_m = new IdentManager();
	public static VariableManager v_m = new VariableManager();
	public static FunctionManager f_m = new FunctionManager();
	public static StateManager s_m = new StateManager();
	public static ExpressionManager e_m = new ExpressionManager();
	public static LineManager l_m = new LineManager();
	
	public static ErrorManager err_m = new ErrorManager();
	
	public static Mem<Type> t_m = new Mem<Type>();
	public static Mem<String> n_m = new Mem<String>();
	public static TextBuffer t_b = new TextBuffer();
	
	public static Emitter e = new Emitter();
	
	
    public static void main(String args[]) { 
        Mittelwerk parser;
        java.io.InputStream input;

        if(args.length >= 1){
            try{
                input = new java.io.FileInputStream(args[0]);
            }catch (java.io.FileNotFoundException e){
                System.out.println(e.getMessage());
                return;
            }
		}else{
            System.out.println("java Mittelwerk <input.mw>");
            return;
        }
		
        try{
            parser = new Mittelwerk(input);
            
            begin(args[0]);
            
            parser.v2();
            System.out.println("analyse syntaxique reussie!");
            
            the_end();
        }catch (ParseException e){
            String msg = e.getMessage();
            System.out.println("Syntax error:\n" + msg);
        } 
    } 
    
    public static void begin(String fileName){
		l_m.setFile(fileName);
    }
    
    public static void the_end(){
		System.out.println(e.getVesselName());
		System.out.println(i_m);
		
		e.emit();
		err_m.finalPrint();
    }
}

PARSER_END(Mittelwerk)


/**************************************/
/*				TOKEN				  */
/**************************************/

TOKEN_MGR_DECLS :
{public static String p_ident, p_string, p_tab, p_state;
 public static int p_int;
 public static double p_double;}

/*** Skip whitespace and comments ***/
SKIP :
{
      " "
    | "\t"
    | "\n" {Mittelwerk.l_m.nextLine();}
    | "\r"
    | "/*"   : IN_COMMENT
}
<IN_COMMENT> MORE:
{
    < ~[] >
}
<IN_COMMENT> SKIP:
{
    < "*/" >  {SwitchTo(DEFAULT);} 
}


/* Reserved words */

TOKEN :
{
    < BOOL: "BOOL" >
    | < DOUBLE: "DOUBLE" >
	| < INT: "INT" >
	| < VOID: "VOID" >
	| < GOTO: "GOTO" >
	| < GOTOGOTO: "GOTOGOTO" >
	| < ACTION: "ACTION" >
	| < FINAL: "FINAL" >
	| < START: "START" >
	
	| < MIN: "MIN" >
	| < MAX: "MAX" >
	
	| < SET_LVL: "SET_LVL" >
	| < GET_LVL: "GET_LVL" >
	| < GET_ALTITUDE: "GET_ALTITUDE" >
	| < API_GET: "API_GET" >
	
	
	| < TRUE: "TRUE" >
	| < FALSE: "FALSE" >
	| < AND: "AND" >
	| < OR: "OR" >
	| < NOT: "NOT" >
	
	| <IF: "IF" >
	| <ELSE: "ELSE" >
	| <WHILE: "WHILE" >
	
	| <RETURN: "RETURN" >
}



/*** number, ident ***/
TOKEN  :
{
    < #digit : ["0"-"9"] >
    | < integer : (<digit>)+ > 
        { p_int = Integer.parseInt(image.toString());}
	| < double_ : (<digit>)+ "." (<digit>)* >
		{ p_double = Double.parseDouble(image.toString());}
	| < #state_c: ["A"-"Z"] | "_" | <digit> >
	| < state: (<state_c>)+ >
		{p_state = image.toString();
		 Mittelwerk.n_m.push(image.toString());}
    | < #letter: ["A"-"Z","a"-"z"] >
	| < #super_letter : <letter> | "_" >
    | < ident : <super_letter> (<super_letter> | <digit>)* >
        {p_ident = image.toString();
		 Mittelwerk.n_m.push(image.toString());}
	| < tab: <ident> "[" <digit> (<digit>)* "]" >
		{p_tab = image.toString();
		 Mittelwerk.n_m.push(image.toString());}
}



/**************************************/
/*				GRAMMAR				  */
/**************************************/

void v2(): {}{
	vesselName()
	(declThrusters())*
	(declFunctions())*
	(declStates())+
	
	<EOF>
}

void vesselName(): {}{
	<ident>
		{e.setVesselName(n_m.pop());}
}

void declThrusters(): {}{
	<ident>
		{i_m.memorizeName(n_m.pop());}
	":"
	<tab>
		{i_m.saveGlobal(Type.THRUSTER, n_m.pop());}
}

void declFunctions(): {}{
		{i_m.clearLocal();}
		{t_b.incTab();}
	typeFunction()
		{f_m.setCurrentType(t_m.pop());}
	<ident>
		{f_m.setCurrentName(n_m.pop());
		 f_m.createFunction();}
	"("
	[args()]
	")"
		{f_m.commitFunction();}
	"{"
	(decl_local_var() ";")*
	[api_get()]
	set_instruction()
	"}"
		{e.add(new I_Simple("}\n\n"));}
		{t_b.decTab();}
}

void args(): {}{
	arg()
	("," arg())*
}

void arg(): {}{
	type()
		{f_m.addParamType(t_m.pop());}
	<ident>
		{f_m.addParamName(n_m.pop());}
}

void set_instruction(): {}{
	(instruction())*
}

void instruction(): {}{
	LOOKAHEAD(2) affect()
	| fct_call() ";" {e.add(new I_Simple(";\n"));}
	| if_()
	| while_()
	| return_()
	| gotogoto() ";"
}

void decl_local_var(): {}{
	type()
		{v_m.memorizeType(t_m.pop());}
	<ident> 
		{v_m.newVar(n_m.pop());}
	("," <ident>
			{v_m.newVar2(n_m.pop());} )*
		{v_m.commitDcl();}
}

void affect(): {}{
		{t_b.reset();}
	<ident> 
		{t_b.add(n_m.pop());}
	"=" 
		{t_b.add("=");}
	valeur()
		{t_b.add(e_m.retrieveStr());}
	 ";"
		{t_b.add(";");}
		{t_b.commit();}
}

void valeur(): {}{
	LOOKAHEAD(2)
		{e_m.init();}
	expression()
		// {e_m.commit();}
	| fct_call()
}


void fct_call(): {}{
		{t_b.commit();}
	<ident> 
		{t_b.add(n_m.pop());}
		"(" 
			{t_b.add("(");}
		(valeur() 
			{t_b.add(e_m.retrieveStr());}
		("," 
			{t_b.add(",");}
		valeur()
			{t_b.add(e_m.retrieveStr());}
			)* )?
			")"
			{t_b.add(")");}
		{t_b.commit();}
	| built_in()
}

void built_in(): {}{
	set_lvl()
	| max()
	| min()
}

void set_lvl(): {}{
		{t_b.reset();}
	<SET_LVL> 
		{t_b.add("SetThrusterLevel");}
	"(" 
		{t_b.add("(");}
	<ident> 
		{t_b.add(((IdentThruster)i_m.getGlobalIdent(n_m.pop())).getValue());}
	"," 
		{t_b.add(",");}
	valeur() 
		{t_b.add(e_m.retrieveStr());}
	")"
		{t_b.add(")");}
		{t_b.commit();}
}

void api_get(): {}{
	<API_GET> "{"
		({t_b.reset();} getters() ";" {t_b.add(";"); t_b.commit();})*
	"}"
}

void getters(): {}{
	LOOKAHEAD(3)
	/* tl0 = GetThrusterLevel(th_hover[0]); */
	<ident> 
		{t_b.add(n_m.pop());} 
	"=" 
		{t_b.add("=");}
	<GET_LVL> 
		{t_b.add("GetThrusterLevel");}
	"(" 
		{t_b.add("(");}
	<ident> 
		{t_b.add(((IdentThruster)i_m.getGlobalIdent(n_m.pop())).getValue());} 
	")"
		{t_b.add(")");}
		
	| /* oapiGetAltitude(GetHandle(), &current_altitude) */
	<ident> "=" <GET_ALTITUDE> 
		{t_b.add("oapiGetAltitude");}
		"(" 
		{t_b.add("(");}
		{t_b.add("GetHandle(), &");}
		{t_b.add(n_m.pop());}
		")"
		{t_b.add(")");}
}

void max(): {}{
	<MAX> "(" 
		{t_b.add("max(");}
	valeur() 
		{t_b.add(e_m.retrieveStr());
		 e_m.init();}
	"," 
		{t_b.add(",");}
	valeur() 
		{t_b.add(e_m.retrieveStr());
		 e_m.init();}
	")"
		{t_b.add(")");}
}

void min(): {}{
	<MIN> "(" 
		{t_b.add("min(");}
	valeur() 
		{t_b.add(e_m.retrieveStr());
		 e_m.init();}
	"," 
		{t_b.add(",");}
	valeur() 
		{t_b.add(e_m.retrieveStr());
		 e_m.init();}
	")"
		{t_b.add(")");}
}



void typeFunction(): {}{
	<VOID> 
		{t_m.push(Type.VOID);}
	| type()
}

void type(): {}{
	<BOOL>
		{t_m.push(Type.BOOL);}
	| <INT>
		{t_m.push(Type.INT);}
	| <DOUBLE>
		{t_m.push(Type.DOUBLE);}
}


void declStates(): {}{
		{i_m.clearLocal();}
		{t_b.incTab();}
	<state> 
		{s_m.addState(n_m.pop());} 
	"{"
		[ <START> ]
		<ACTION> "{"
			set_instruction()
		"}"
		jmp()
	"}"
		{e.add(new I_Simple("}\n\n"));}
		{t_b.decTab();}
}

void jmp(): {}{
	<FINAL>
	| <GOTO> "{"
		// (if_goto())+
		set_instruction()
		"}"
}


void if_(): {}{
		{t_b.reset();}
	<IF> "(" 
		{t_b.add("if(");}
		valeur() 
			{t_b.add(e_m.retrieveStr());}
		")" "{"
			{t_b.add("){");}
			{t_b.incTab();}
			{t_b.commit();}
		set_instruction()
	"}"
		{t_b.decTab();}
		{t_b.reset();
		 t_b.add("}");
		 t_b.commit();}
	( <ELSE> ("{" set_instruction() "}"
		| instruction()))?
		{e.add(new I_Simple("\n\n"));}
}

void if_goto(): {}{
		{t_b.reset();}
	<IF> "(" 
		{t_b.add("if(");}
		valeur() 
			{t_b.add(e_m.retrieveStr());}
		")" "{"
			{t_b.add("){");}
			{t_b.commit();}
			{t_b.incTab();}
		set_instruction()
	"}"
		{t_b.decTab();}
		{t_b.reset();
		 t_b.add("}");
		 t_b.commit();}
	( <ELSE> ("{" set_instruction() "}"
		| gotogoto() ";"))?
		{e.add(new I_Simple("\n\n"));}
}


/* void set_instruction_goto(): {}{
	(instruction()
	| gotogoto() ";")*
} */

void gotogoto(): {}{
	<GOTOGOTO> <state>
		{s_m.gotogoto(n_m.pop());}
}

void while_(): {}{
		{t_b.reset();}
	<WHILE>
	"("
		{t_b.add("while(");}
	valeur()
		{t_b.add(e_m.retrieveStr());}
	")"
	"{"
		{t_b.add("){");}
		{t_b.commit();}
		{t_b.incTab();}
		
		set_instruction()
	"}"
		{t_b.decTab();}
		{t_b.reset();
		 t_b.add("}");
		 t_b.commit();}
}

void return_(): {}{
		{t_b.reset();}
	<RETURN>
		{t_b.add("return");}
	valeur()
		{t_b.add(e_m.retrieveStr());}
	";"
		{t_b.add(";");}
		{t_b.commit();}
}










/*
 * Expression .
 */

void expression() : {}
{
    simpleExpr() 
    (opRel() 
    simpleExpr() 
    )?  
		
}

void simpleExpr() : {}
{
    terme() 
    (opAdd() 
    terme()	
    )*
} 

void terme() : {}
{
    facteur() (opMul() facteur() )*
}

void facteur() : {}
{  
    primaire() 
    | opNeg() primaire()
}

void primaire (): {}
{
    value()
    | "(" {e_m.addStr("(");} expression() ")" {e_m.addStr(")");}
}

void value () : {}
{ 
    <integer> {e_m.addInt(MittelwerkTokenManager.p_int);}
	| <double_> {e_m.addDouble(MittelwerkTokenManager.p_double);}
    | <ident> {e_m.addIdent(MittelwerkTokenManager.p_ident);}
    | <TRUE> 
    | <FALSE>
}

void opRel() : {}
{
    "==" {e_m.addStr("==");}
    | "!=" {e_m.addStr("!=");}
    | "<" {e_m.addStr("<");}
    | "<=" {e_m.addStr("<=");}
    | ">" {e_m.addStr(">");}	
    | ">=" {e_m.addStr(">=");}
} 

void opAdd() : {}
{
    "+"	{e_m.addStr("+");}
    |  "-"  {e_m.addStr("-");}
    | <OR> 	{e_m.addStr("||");}
}

void opMul() : {}
{
    "*" {e_m.addStr("*");}
    | "/" {e_m.addStr("/");}		
    | <AND> {e_m.addStr("&&");}
} 

void opNeg() : {}
{ 
    "-" {e_m.addStr("-");}
    | <NOT> {e_m.addStr("!");}
}







